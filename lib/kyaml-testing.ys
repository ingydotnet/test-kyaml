!YS-v0

ns: kyaml-testing

use ys::taptest: :all

defn kyaml-render(_ test):
  resp =: sh({:in test.data} 'yaml_renderer')

  # Debugging tests:
  cond:
    test.SHOW == 'resp':
      WWW: resp
    test.SHOW:
      WWW: resp.out

  if resp.exit == 0:
    then:
      want =: test.want
      got =: resp.out
      when-not got == want:
        show-diff(test.want resp.out test.name)
      =>: got
    else:
      =>: resp.err

defn test-kyaml(tests):
  test:
    mapv _ tests:
      fn(t)::
        name:: t.name
        form:: kyaml-render
        data:: t.yaml
        want:: t.kyaml
        :when t.SKIP:: {SKIP: true}
        :when t.ONLY:: {ONLY: true}

defn show-diff(want got name):
  tmp =: ENV.TMP
  want-file =: "$tmp/want"
  got-file =: "$tmp/got"
  write want-file: want
  write got-file: got
  diff =: sh("diff -u $want-file $got-file").out
  warn: |+


    === Diff for '$name' ===
    $diff
